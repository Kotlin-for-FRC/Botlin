import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2025.0'
    id "org.jetbrains.kotlin.jvm" version "2.1.0" apply false
    id "io.deepmedia.tools.deployer" version "0.18.0" apply false
}

// WPILib Version
ext.wpilibVersion = "2025.+"

allprojects {
    repositories {
        mavenCentral()
    }

    if (project.hasProperty('releaseMode')) {
        wpilibRepositories.addAllReleaseRepositories(project)
    } else {
        wpilibRepositories.addAllDevelopmentRepositories(project)
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    var javaVersion = "17"
    def kotlinJvmTarget = JvmTarget.fromTarget(javaVersion.toString())

    kotlin {
        compilerOptions {
            jvmTarget.set(kotlinJvmTarget)
            jvmToolchain(Integer.valueOf(javaVersion.toString()))
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add '-XDstringConcat=inline'
        options.encoding = 'UTF-8'
    }

    // Setup Javadocs to link back to WPILib docs
    javadoc {
        options {
            links "https://docs.oracle.com/en/java/javase/$javaVersion/docs/api/", 'https://github.wpilib.org/allwpilib/docs/release/java/'
        }
    }
}

wrapper {
    gradleVersion '8.11'
}
