import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'maven-publish'
apply plugin: 'io.deepmedia.tools.deployer'

ext.licenseFile = files("$rootDir/LICENSE.txt")

def templateVendorFile = "Botlin.json"

def pubVersion = property('version')

def outputsFolder = file("$buildDir/outputs")

def versionFile = file("$outputsFolder/version.txt")

task outputVersions() {
    description = 'Prints the versions of wpilib to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

task libraryBuild() {}

build.dependsOn outputVersions

task copyAllOutputs(type: Copy) {
    destinationDir file("$buildDir/allOutputs")
    from versionFile
    dependsOn outputVersions
}

build.dependsOn copyAllOutputs
copyAllOutputs.dependsOn outputVersions

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archiveFile
    copyAllOutputs.from task.archiveFile
}

def artifactGroupId = 'gay.zharel.botlin'
def baseArtifactId = 'botlin'
def javaBaseName = "_GROUP_gay_zharel_botlin_ID_${baseArtifactId}-java_CLS"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task outputJar(type: Jar, dependsOn: classes) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    from sourceSets.main.output
}

task outputSourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task outputJavadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// Apply template variables from the vendordep file.
// Replaces ${VARIABLE} with VARIABLE: value in expand()
task vendordepJson() {
    description = 'Builds the vendordep json file.'
    group = 'Build'
    outputs.file("$buildDir/repos/$templateVendorFile")

    copy {
        from templateVendorFile
        into "$buildDir/repos/"
        expand(version: pubVersion,
                groupId: artifactGroupId,
                artifactId: baseArtifactId)
    }
}

task vendordepJsonZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = "vendordepJson"

    from("$buildDir/repos/$templateVendorFile") {
        into '/'
    }
    dependsOn vendordepJson
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives outputJar
    archives outputSourcesJar
    archives outputJavadocJar
}

addTaskToCopyAllOutputs(outputSourcesJar)
addTaskToCopyAllOutputs(outputJavadocJar)
addTaskToCopyAllOutputs(outputJar)
addTaskToCopyAllOutputs(vendordepJsonZip)

build.dependsOn outputSourcesJar
build.dependsOn outputJavadocJar
build.dependsOn outputJar
build.dependsOn vendordepJsonZip

libraryBuild.dependsOn build

def releasesRepoUrl = "$buildDir/repos/releases"

publishing {
    repositories {
        maven {
            url = releasesRepoUrl
        }
    }
}

task cleanReleaseRepo(type: Delete) {
    delete releasesRepoUrl
}

tasks.matching {it != cleanReleaseRepo}.all {it.dependsOn cleanReleaseRepo}

publishing {
    publications {
        java(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar

            artifactId = "${baseArtifactId}-java"
            groupId artifactGroupId
            version pubVersion
        }
    }
}

deployer {
    projectInfo {
        groupId.set(artifactGroupId)
        artifactId.set(baseArtifactId)
        url.set("https://github.com/Kotlin-for-FRC/Botlin")

        scm {
            fromGithub("Kotlin-for-FRC", "Botlin")
        }

        license("BSD-3-Clause", "https://opensource.org/license/bsd-3-clause")

        developer("Zachary Harel", "ftc@zharel.me", null, "https://github.com/zachwaffle4")
    }

    signing {
        key.set(secret("MVN_GPG_KEY"))
        password.set(secret("MVN_GPG_PASSWORD"))
    }

    content {
        component {
            fromJava()
        }
    }

    localSpec("local") {
        release.version = "$pubVersion"
    }

    centralPortalSpec("centralPortal") {
        release.version = "$pubVersion"
        auth.user.set(secret("SONATYPE_USERNAME"))
        auth.password.set(secret("SONATYPE_PASSWORD"))
    }
}
